plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'java'
}


ext {
    apacheCommonIOVersion = "2.11.0"
    mapstructVersion = "1.4.2.Final"
    lombokMapstructBindingVersion = "0.2.0"
    mybatisVersion = "2.2.0"
    pagehelperVersion = "1.4.1"
}

group = 'com.circler'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
targetCompatibility = '11'

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }

    dependencies {
        dependency "commons-io:commons-io:${apacheCommonIOVersion}"
        dependency "org.mapstruct:mapstruct:${mapstructVersion}"
        dependency "org.mapstruct:mapstruct-processor:${mapstructVersion}"
        dependency "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
        dependency "org.mybatis.spring.boot:mybatis-spring-boot-starter:${mybatisVersion}"
        dependency "com.github.pagehelper:pagehelper-spring-boot-starter:${pagehelperVersion}"
    }
}

dependencies {

    implementation "org.apache.commons:commons-pool2"
    implementation "org.apache.commons:commons-lang3"
    implementation 'commons-io:commons-io'

    implementation("org.springframework.boot:spring-boot-starter-web") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-tomcat"
    }
    implementation("org.springframework.boot:spring-boot-starter-undertow")

    implementation "org.springframework.boot:spring-boot-starter-validation"

    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter'
    implementation "com.github.pagehelper:pagehelper-spring-boot-starter"
    implementation "mysql:mysql-connector-java"

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus:latest.release'

    implementation "org.mapstruct:mapstruct", "org.projectlombok:lombok"
    annotationProcessor "org.mapstruct:mapstruct-processor", "org.projectlombok:lombok", "org.projectlombok:lombok-mapstruct-binding"
    testImplementation "org.mapstruct:mapstruct", "org.projectlombok:lombok"
    testAnnotationProcessor "org.mapstruct:mapstruct-processor", "org.projectlombok:lombok", "org.projectlombok:lombok-mapstruct-binding"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}
